symbol = @{ ASCII_ALPHA ~ ( ASCII_ALPHA | ASCII_DIGIT | "_" )* }
float = @{ ( "+" | "-" )? ~ ASCII_DIGIT ~ "." ~ ASCII_DIGIT* ~ ( "E" ~ "-"? ~ ASCII_DIGIT+ )? }
string = @{ "'" ~ inner ~ "'" }
inner = @{ char* }
char = {
	!("'" | "\\") ~ ANY
	| "\\" ~ ("'" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
	| "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
id = @{ "#" ~ ASCII_DIGIT+ }
control = @{ "." ~ (ASCII_ALPHA | "_" | ".")+ }

WHITESPACE = _{ " " | "\r" | "\t" | "\n" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

ign = _{ (WHITESPACE | COMMENT)* }

tuple = ${ "(" ~ ign ~ value ~ ign ~ ("," ~ ign ~ value ~ ign)* ~ ")" }
value = { tuple | symbol | float | string | id | control }

desc = { symbol ~ "(" ~ value ~ ("," ~ value)* ~ ")" ~ ";" }
