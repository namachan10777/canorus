HEX = { "A" | "B" | "C" | "D" | "E" | "F" | ASCII_DIGIT }

symbol = @{ ASCII_ALPHA ~ ( ASCII_ALPHA | ASCII_DIGIT | "_" | "-" )* }
float = @{ ( "+" | "-" )? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT* ~ ( "E" ~ "-"? ~ ASCII_DIGIT+ )? }
integer = @{ ASCII_DIGIT+ }
string = @{ "'" ~ inner ~ "'" }
inner = @{ char* }
char = @{ uniescape | "\\'" | (!"'" ~ ANY) }
uniescape = @{ "\\X2\\" ~ HEX* ~ "\\X0\\" }
wildcard = { "*" }
dollar = { "$" }
id = @{ "#" ~ ASCII_DIGIT+ }
control = @{ "." ~ (ASCII_ALPHA | "_" | ".")+ }

WHITESPACE = _{ " " | "\r" | "\t" | "\n" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

ign = _{ (WHITESPACE | COMMENT)* }

tuple = { "(" ~ value  ~ ("," ~ value )* ~ ")" }
desc = { symbol ~ "(" ~ ( value ~ ("," ~ value)* )? ~ ")" }
value = { tuple | string | float | integer | id | control | wildcard | dollar | desc | symbol }

elem = { id ~ "=" ~ value ~ ";"}
header = { "HEADER;" ~ ( value ~ ";" )+ ~ "ENDSEC;" }
data = { "DATA;" ~ elem+ ~ "ENDSEC;" }
step = { SOI ~ "ISO-10303-21;" ~ header ~ data ~ "END-ISO-10303-21;" ~ EOI }
