symbol = @{ ASCII_ALPHA ~ ( ASCII_ALPHA | ASCII_DIGIT | "_" | "-" )* }
float = @{ ( "+" | "-" )? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT* ~ ( "E" ~ "-"? ~ ASCII_DIGIT+ )? }
string = @{ "'" ~ inner ~ "'" }
inner = @{ char* }
char = {
	"\\'" | !"'" ~ ANY
}
id = @{ "#" ~ ASCII_DIGIT+ }
control = @{ "." ~ (ASCII_ALPHA | "_" | ".")+ }

WHITESPACE = _{ " " | "\r" | "\t" | "\n" }
COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

ign = _{ (WHITESPACE | COMMENT)* }

tuple = ${ "(" ~ ign ~ value ~ ign ~ ("," ~ ign ~ value ~ ign)* ~ ")" }
value = { tuple | symbol | float | string | id | control }

desc = { symbol ~ "(" ~ value ~ ("," ~ value)* ~ ")" ~ ";" }
elem = { id ~ "=" ~ desc }
header = { "HEADER;" ~ desc+ ~ "ENDSEC;" }
data = { "DATA;" ~ elem+ ~ "ENDSEC;" }
step = { SOI ~ "ISO-10303-21;" ~ header ~ data ~ "END-ISO-10303-21;" ~ EOI }
